CONSTRUCT {
  ?class rdf:type owl:Class;
         rdfs:label ?classLabel;
         schema:description ?classDescription;
         skos:altLabel ?classAlias;
         rdfs:subClassOf ?superClass;
         owl:equivalentClass ?equivalentClass.
} WHERE {
  hint:Query hint:optimizer "None".

  {
    VALUES ?top { wd:Q2188189 wd:Q115669410 wd:Q110879422 wd:Q18127 wd:Q32178211 wd:Q638 wd:Q65007448 wd:Q746359 wd:Q115927938 wd:Q15190726 wd:Q1760309 wd:Q5057302 }
    ?class wdt:P279* ?top.
  } UNION {
    VALUES ?theme { wd:Q638 wd:Q164204 }
    ?class wdt:P279*/(wdt:P1269|wdt:P2579) ?theme.
  } UNION {
    ?class wdt:P6104 wd:Q5830855.
  }

  ?class wdt:P279 ?superClass;
         rdfs:label ?classLabel FILTER(lang(?classLabel) = 'en').
  OPTIONAL { ?class schema:description ?classDescription FILTER(lang(?classDescription) = 'en'). }
  OPTIONAL { ?class skos:altLabel ?classAlias FILTER(lang(?classAlias) = 'en'). }
  OPTIONAL { ?class wdt:P1709 ?equivalentClass. }
}
ORDER BY ?class
