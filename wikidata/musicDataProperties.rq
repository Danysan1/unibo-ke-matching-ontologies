CONSTRUCT {
  ?prop rdf:type owl:DataTypeProperty;
         rdfs:label ?propLabel;
        #  rdfs:domain ?domain;
         rdfs:range ?range;
         schema:description ?propDescription;
         skos:altLabel ?propAlias;
         owl:equivalentProperty ?equivalentProp;
         rdfs:subPropertyOf ?superProp.
  # ?domain rdf:type owl:Class;
  #         rdfs:label ?domainLabel;
  #         owl:unionOf ( ?domainElement ).
} WHERE {
  {
    ?prop wdt:P31/wdt:P279* wd:Q27525351.
  } UNION {
    ?prop wdt:P6104 wd:Q5830855.
  }

  VALUES (?type ?range) { # https://www.wikidata.org/wiki/Help:Data_type#Technical_details
    (wikibase:Quantity xsd:decimal)
    (wikibase:Time xsd:dateTime)
    (wikibase:Monolingualtext xsd:string)
    (wikibase:String xsd:string)
    (wikibase:MusicalNotation xsd:string)
  }
  ?prop wikibase:propertyType ?type;
        rdfs:label ?propLabel FILTER(lang(?propLabel) = 'en').
  
  # BIND(IRI(REPLACE(
  #     STR(?prop),
  #     'http://www.wikidata.org/entity/',
  #     'https://raw.githubusercontent.com/Danysan1/unibo-ke-matching-ontologies/main/wikidata/ontology/domain/'
  #   )) AS ?domain).
  # BIND(REPLACE(STR(?prop),'http://www.wikidata.org/entity/','domain of ') AS ?domainLabel).
  # OPTIONAL {
  #   ?prop p:P2302 [
  #     ps:P2302 wd:Q21503250;
  #     pq:P2308 ?domainElement;
  #     pq:P2309 wd:Q21503252
  #   ].
  # }

  OPTIONAL { ?prop schema:description ?propDescription FILTER(lang(?propDescription) = 'en'). }
  OPTIONAL { ?prop skos:altLabel ?propAlias FILTER(lang(?propAlias) = 'en'). }
  OPTIONAL { ?prop wdt:P1628 ?equivalentProp. }
  OPTIONAL { ?prop wdt:P1647 ?superProp. }
}
ORDER BY ?prop