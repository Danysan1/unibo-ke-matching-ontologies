SELECT
  ?class
  ?classLabel
  ?classDescription 
  (GROUP_CONCAT(DISTINCT ?superClassLabel;SEPARATOR=' // ') AS ?superClasses)
  (GROUP_CONCAT(DISTINCT STR(?equivalentClass);SEPARATOR=' // ') AS ?equivalentClasses)
WHERE
{
  VALUES ?top { wd:Q2431196 wd:Q2188189 wd:Q115669410 wd:Q110879422 wd:Q18127 wd:Q32178211 }
  ?class wdt:P279+ ?top;
            wdt:P279+ ?superClass.
  ?superClass wdt:P279* ?top.
  OPTIONAL {
    ?class wdt:P1709 ?equivalentClass.
  }
  SERVICE wikibase:label {
    bd:serviceParam wikibase:language "en,fr".
    ?class rdfs:label ?classLabel.
    ?class schema:description ?classDescription.
    ?superClass rdfs:label ?superClassLabel.
  } 
}
GROUP BY ?class ?classLabel ?classDescription