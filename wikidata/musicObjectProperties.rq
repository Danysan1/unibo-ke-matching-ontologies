CONSTRUCT {
  ?prop rdf:type owl:ObjectProperty;
         rdfs:label ?propLabel;
         rdfs:domain ?domain;
         rdfs:range ?range;
         schema:description ?propDescription;
         skos:altLabel ?propAlias;
         owl:equivalentProperty ?equivalentProp;
         rdfs:subPropertyOf ?superProp;
         owl:inverseOf ?inverseProp.
} WHERE {
  VALUES ?type { # https://www.wikidata.org/wiki/Help:Data_type#Technical_details
    wikibase:WikibaseItem wikibase:ExternalId
  }
  ?prop wikibase:propertyType ?type;
        wdt:P31/wdt:P279* wd:Q27525351;
        rdfs:label ?propLabel FILTER(lang(?propLabel) = 'en').
  
  OPTIONAL {
    ?prop p:P2302 [
      ps:P2302 wd:Q21503250;
      pq:P2308 ?domain
    ].
  }
  OPTIONAL {
    ?prop p:P2302 [
      ps:P2302 wd:Q21510865;
      pq:P2308 ?range
    ].
  }

  OPTIONAL { ?prop schema:description ?propDescription FILTER(lang(?propDescription) = 'en'). }
  OPTIONAL { ?prop skos:altLabel ?propAlias FILTER(lang(?propAlias) = 'en'). }
  OPTIONAL { ?prop wdt:P1628 ?equivalentProp. }
  OPTIONAL { ?prop wdt:P1647 ?superProp. }
  OPTIONAL { ?prop wdt:P1696 ?inverseProp. }
}
ORDER BY ?prop